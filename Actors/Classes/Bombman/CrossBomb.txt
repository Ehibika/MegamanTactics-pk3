actor Ability2_Bombman : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "CrossBomb Ability"
weapon.ammotype1 "AbilityAmmo"
inventory.icon "FIRESI"
Dropitem ""
+WEAPON.NOAUTOFIRE
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
Weapon.SlotPriority 5
States
{

Ready:
BOMT D 0 
"####" D 1 Offset(0,110)
"####" D 1 Offset(0,96)
"####" D 1 Offset(0,82)
"####" D 1 Offset(0,70)
"####" D 1 Offset(0,58)
"####" D 1 Offset(0,46)
"####" D 1 Offset(0,32)
Ready1:
"####" D 0 A_JumpIf(pitch>placeangle && pitch<=90 && z-floorz < 32, "Ready2")
"####" D 0 A_FireCustomMissile("HyperBombAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" D 1 A_WeaponReady
goto Ready1

Ready2:
"####" D 0 A_FireCustomMissile("RushCallRingSpawner",0,0,0,0)
"####" D 1 A_WeaponReady
goto Ready1

Fire:
"####" D 0 A_JumpIf(pitch>placeangle && pitch<=90 && z-floorz < 32, "Placed")
"####" EEFF 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" D 0 A_FireCustomMissile("CrossBomb",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" D 0 A_TakeInventory("AbilityCharge_2",1)
"####" GHIJKL 2
"####" P 10
"####" D 0 A_Jump(256,"End")
goto End

AltFire:
"####" D 0 A_JumpIf(pitch>placeangle && pitch<=90 && z-floorz < 32, "Placed")
"####" MMNN 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" D 0 A_SpawnItemEX("BowlingCrossBomb",0,0,-(z-floorz)+5,cos(pitch), 0, sin(-pitch), 0, SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
"####" D 0 A_TakeInventory("AbilityCharge_2",1)
"####" D 0 
"####" E 0 A_GiveInventory("SignatureRecharge_20") //
"####" OO 2
"####" P 10
"####" D 0 A_Jump(256,"End")
goto End

Placed:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" D 0 A_FireCustomMissile("PlacedCrossBombSpawner",0,0,0)
"####" D 0 A_TakeInventory("AbilityCharge_2",1)
"####" D 0 
"####" E 0 A_GiveInventory("SignatureRecharge_20") //
"####" CBA 2
"####" A 10
"####" D 0 A_Jump(256,"End")
goto End

End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End
}
}
//====

actor UseAbility2_Bombman : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("CrossBombAbility",1)
fail
}
}

actor CrossBombAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_2",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop
}
}
//=================
//=================


actor CrossBomb : Hyperbomb_Boss
{
States
{

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
BOMB S 1 A_SpawnItemEX("CrossBomb2",0,0,0,momx*0.1,momy*0.1,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
MINB A 0 A_SpawnItemEx("CrossBombExplosion")
stop
}
}

actor CrossBomb2 : CrossBomb
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
BOMB SS 1 A_GiveInventory("HyperBombFunc")
BOMB SS 0 A_CountDown

BOMB WW 1 A_GiveInventory("HyperBombFunc")
BOMB WW 0 A_CountDown
loop
	
Death:
MINB A 0 A_SpawnItemEx("CrossBombExplosion",0,0,0,0,0,0,0,1)
stop
}
}

actor BowlingCrossBomb : CrossBomb
{
-NOGRAVITY
//-DOOMBOUNCE
-BOUNCEONFLOORS
-USEBOUNCESTATE
+BOUNCEONWALLS
+STEPMISSILE
seesound "weapon/megaball"
MaxStepHeight 32
speed 35
gravity 1.0
Damage (0)
wallbouncefactor 1.0
BounceFactor 1.0
reactiontime 35
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB SS 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB SS 0 A_CountDown
BOMB TT 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB TT 0 A_CountDown
BOMB UU 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB UU 0 A_CountDown
BOMB TT 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB TT 0 A_CountDown
Goto Spawn2

Death:
MINB A 0 A_SpawnItemEx("CrossBombExplosion")
stop
}
}

actor PlacedCrossBombSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
WEAP B 0 A_SpawnItemEx("PlacedCrossBomb", 0, 0, -(z-floorz)+0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 0)
stop
}
}

actor PlacedCrossBomb
{
damagetype "HyperBomb"
Obituary "$OB_Hyperbomb"
radius 4
height 4
scale 2.0
+MISSILE
-NOGRAVITY
+MTHRUSPECIES
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
speed 0
gravity 1.0
Damage (0)
reactiontime 175
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB SS 1 A_JumpIF(reactiontime < 37,"Spawn3") //A_GiveInventory("HyperBombFunc")
BOMB SS 0 A_CountDown
BOMB VV 1 A_JumpIF(reactiontime < 37,"Spawn3") //A_GiveInventory("HyperBombFunc")
BOMB VV 0 A_CountDown
Goto Spawn2

Spawn3:
BOMB SS 1 A_PlaySoundEX("weapon/Heat1","Soundslot6")
BOMB SS 0 A_CountDown
BOMB WW 1 A_PlaySoundEX("weapon/Heat1","Soundslot6")
BOMB WW 0 A_CountDown
Goto Spawn3

Death:
MINB A 0 A_SpawnItemEx("CrossBombExplosion")
stop
}
}

actor CrossBombExplosion : BasicExplosion
{
damagetype "HyperBomb"
Obituary "$OB_Hyperbomb"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEX("CrossBombFire",0,0,-(z-floorz)+5,cos(pitch), 0, sin(-pitch), 0, SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE)
TNT1 A 0 A_SpawnItemEX("CrossBombFire",0,0,-(z-floorz)+5,cos(pitch), 0, sin(-pitch), 90, SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE)
TNT1 A 0 A_SpawnItemEX("CrossBombFire",0,0,-(z-floorz)+5,cos(pitch), 0, sin(-pitch), 270, SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE)
TNT1 A 0 A_SpawnItemEX("CrossBombFire",0,0,-(z-floorz)+5,cos(pitch), 0, sin(-pitch), 180, SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE)
TNT1 A 0 A_Playsoundex("weapon/HyperBomb","Soundslot5")
stop
}
}



actor CrossBombFire
{
+MISSILE
+RIPPER
+DONTREFLECT
+FLOORHUGGER
+SLIDESONWALLS
Damage (0)
Speed 20
args 20 //Same as speed
Height 48
Radius 10
Scale 2.5
ReactionTime 50
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 0 A_SpawnItemEx("CrossBombFireWave")
TNT1 A 0 A_PlaySoundEx("weapon/Firestorm","Weapon")
TNT1 A 2 A_CountDown
Goto Spawn2
Death:
TNT1 A 0 
stop
}
}


actor CrossBombFireWave
{
PROJECTILE
Obituary "$OB_MELTCREEPER"
DamageType "MeltCreeper"
+DONTBLAST
+DONTREFLECT
+RIPPER
+FLOORHUGGER
Radius 20
Height 26
Damage (12)
Speed 0
YScale 2.5
XScale 2.5
reactiontime 35
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
FWVE UVW 2 
FWVE X 0 A_PlaySoundEx("weapon/FireManWave","Weapon")
Spawn2:
FWVE XY 2
FWVE X 0 A_Countdown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
FWVE W 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
FWVE WVU 2
stop
}
}