const int placeangle = 45;

actor Ability1_Bombman : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "HyperBomb Ability"
weapon.ammotype1 "AbilityAmmo"
inventory.icon "BOMBSI"
Dropitem ""
+WEAPON.NOAUTOFIRE
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
Weapon.SlotPriority 5
States
{

Ready:
BOMT D 0 
"####" D 1 Offset(0,110)
"####" D 1 Offset(0,96)
"####" D 1 Offset(0,82)
"####" D 1 Offset(0,70)
"####" D 1 Offset(0,58)
"####" D 1 Offset(0,46)
"####" D 1 Offset(0,32)
Ready1:
"####" D 0 A_JumpIf(pitch>placeangle && pitch<=90 && z-floorz < 32, "Ready2")
"####" D 0 A_FireCustomMissile("HyperBombAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" D 1 A_WeaponReady
goto Ready1

Ready2:
"####" D 0 A_FireCustomMissile("RushCallRingSpawner",0,0,0,0)
"####" D 1 A_WeaponReady
goto Ready1

Fire:
"####" D 0 A_JumpIf(pitch>placeangle && pitch<=90 && z-floorz < 32, "Placed")
"####" EEFF 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" D 0 A_FireCustomMissile("Hyperbomb_Boss",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" D 0 A_TakeInventory("AbilityCharge_1",1)
"####" D 0 
"####" E 0 A_GiveInventory("SignatureRecharge_20") //
"####" GHIJKL 2
"####" P 10
"####" D 0 A_Jump(256,"End")
goto End

AltFire:
"####" D 0 A_JumpIf(pitch>placeangle && pitch<=90 && z-floorz < 32, "Placed")
"####" MMNN 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" D 0 A_SpawnItemEX("BowlingHyperBomb",0,0,-(z-floorz)+5,cos(pitch), 0, sin(-pitch), 0, SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
"####" D 0 A_TakeInventory("AbilityCharge_1",1)
"####" D 0 
"####" E 0 A_GiveInventory("SignatureRecharge_20") //
"####" OO 2
"####" P 10
"####" D 0 A_Jump(256,"End")
goto End

Placed:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" D 0 A_FireCustomMissile("PlacedHyperbombSpawner",0,0,0)
"####" D 0 A_TakeInventory("AbilityCharge_1",1)
"####" D 0 
"####" E 0 A_GiveInventory("SignatureRecharge_20") //
"####" CBA 2
"####" A 10
"####" D 0 A_Jump(256,"End")
goto End

End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End
}
}
//====

actor UseAbility1_Bombman : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("MegaHyperBombAbility",1)
fail
}
}

actor MegaHyperBombAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_1",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop
}
}
//=================
//=================
actor HyperBombAimPath : GolfBallAimInstantStarter3 { Speed 56 gravity 1.0}

actor Hyperbomb_Boss 
{
PROJECTILE
damagetype "HyperBomb"
Obituary "$OB_Hyperbomb"
radius 4
height 4
scale 2.0
-NOGRAVITY
+DOOMBOuNCE
+BOUNCEONCEILINGS
+ALLOWBOUNCEONACTORS
+BOUNCEONACTORS
+MTHRUSPECIES
+USEBOUNCESTATE
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
+MBFBOUNCER
+CANBOUNCEWATER
speed 56
gravity 1.0
Damage (0)
//wallbouncefactor 0.5
BounceFactor 0.3
reactiontime 35
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB SS 1 A_GiveInventory("HyperBombFunc")
BOMB SS 0 A_CountDown
BOMB VV 1 A_GiveInventory("HyperBombFunc")
BOMB VV 0 A_CountDown
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
BOMB S 1 //A_SpawnItemEX("Hyperbomb_Boss2",0,0,0,momx*0.1,momy*0.1,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
goto Spawn3

Spawn3:
BOMB SS 1 A_GiveInventory("HyperBombFunc")
BOMB SS 0 A_CountDown

BOMB WW 1 A_GiveInventory("HyperBombFunc")
BOMB WW 0 A_CountDown
Goto Spawn3

Death:
MINB A 0 A_SpawnItemEx("HYPERBOMB_BossExplosion")
stop
}
}
/*
actor Hyperbomb_Boss2 : Hyperbomb_Boss
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
BOMB SS 1 A_GiveInventory("HyperBombFunc")
BOMB SS 0 A_CountDown

BOMB WW 1 A_GiveInventory("HyperBombFunc")
BOMB WW 0 A_CountDown
loop
	
Death:
MINB A 0 A_SpawnItemEx("HYPERBOMB_BossExplosion",0,0,0,0,0,0,0,1)
stop
}
}
*/
//====

actor HyperBombFunc : CustomInventory
{
states
{
Pickup:
BOMB S 1 A_SpawnItemEX("S_HyperbombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BOMB S 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
BOMB S 0 A_CheckFloor("SlowDown")
stop
SlowDown:
BOMB S 0 A_ScaleVelocity(0.3)
stop
}
}

actor BowlingHyperbomb : Hyperbomb_Boss
{
-NOGRAVITY
//-DOOMBOUNCE
-BOUNCEONFLOORS
-USEBOUNCESTATE
+BOUNCEONWALLS
+STEPMISSILE
seesound "weapon/megaball"
MaxStepHeight 32
speed 35
gravity 1.0
Damage (0)
wallbouncefactor 1.0
BounceFactor 1.0
reactiontime 35
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB SS 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB SS 0 A_CountDown
BOMB TT 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB TT 0 A_CountDown
BOMB UU 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB UU 0 A_CountDown
BOMB TT 1 A_GiveInventory("BowlingHyperBombFunc")
BOMB TT 0 A_CountDown
Goto Spawn2

Death:
MINB A 0 A_SpawnItemEx("HYPERBOMB_BossExplosion")
stop
}
}

actor BowlingHyperBombFunc : CustomInventory
{
states
{
Pickup:
BOMB S 1 A_SpawnItemEX("S_HyperbombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BOMB S 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
stop
}
}

actor PlacedHyperbombSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
WEAP B 0 A_SpawnItemEx("PlacedHyperbomb", 0, 0, -(z-floorz)+0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 0)
stop
}
}

actor PlacedHyperbomb
{
damagetype "HyperBomb"
Obituary "$OB_Hyperbomb"
radius 4
height 4
scale 2.0
+MISSILE
-NOGRAVITY
+MTHRUSPECIES
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
speed 0
gravity 1.0
Damage (0)
reactiontime 175
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB SS 1 A_JumpIF(reactiontime < 37,"Spawn3") //A_GiveInventory("HyperBombFunc")
BOMB SS 0 A_CountDown
BOMB VV 1 A_JumpIF(reactiontime < 37,"Spawn3") //A_GiveInventory("HyperBombFunc")
BOMB VV 0 A_CountDown
Goto Spawn2

Spawn3:
BOMB SS 1 A_PlaySoundEX("weapon/Heat1","Soundslot6")
BOMB SS 0 A_CountDown
BOMB WW 1 A_PlaySoundEX("weapon/Heat1","Soundslot6")
BOMB WW 0 A_CountDown
Goto Spawn3

Death:
MINB A 0 A_SpawnItemEx("HYPERBOMB_BossExplosion")
stop
}
}

actor Hyperbomb_BossExplosion : BasicExplosion
{
damagetype "HyperBomb"
Obituary "$OB_Hyperbomb"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
//TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX_H0",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX_H1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX_H2",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX_H3",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX_H4",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX_H5",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX_H6",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",0,0,2,0,0,17,0,33)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",0,0,-2,0,0,-17,0,33)
//110
NAPC D 0 A_Explode(90, 360, 1,0,216)
TNT1 A 0 A_Playsoundex("weapon/HyperBomb","Soundslot5")
TNT1 A 17 A_Quake(6,17,0,300)
stop
}
}

actor HyperbombBlastFX_H0 : BasicClientSide
{
renderstyle none
Reactiontime 12//Sections
Args 20,6
//Speed/lines
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetArg(3,Args[1]+1)
TNT1 A 0 A_SetArg(4,Reactiontime)
SpawnL:
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",Cos(90-180/Args[3]*Args[1])*Args[0],0,Sin(90-180/Args[3]*Args[1])*Args[0],Cos(90-180/Args[3]*Args[1])*Args[0],0,Sin(90-180/Args[3]*Args[1])*Args[0],360/Args[4]*Reactiontime,33)
TNT1 A 0 A_SetArg(1,Args[1]-1)
TNT1 A 0 A_JumpIf(Args[1]<=0,1)
loop
TNT1 A 0 A_Countdown
TNT1 A 0 A_SetArg(1,Args[3]-1)
loop
Death:
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",0,0,Args[0],0,0,Args[0],0,33)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",0,0,-Args[0],0,0,-Args[0],0,33)
TNT1 A 1
stop
}
}

actor HyperbombBlastFX_H1 : BasicClientSide
{
var int user_RT;
var int user_A;

Mass 100
MeleeRange 0
accuracy 6
renderstyle none
reactiontime 15//18
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_RT",360/reactiontime)
TNT1 A 0 A_SetUserVar("user_A",random(0,user_RT))
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",Mass/accuracy,0,0,Mass/accuracy,0,MeleeRange/accuracy,user_A,37)
TNT1 A 0 A_SetUserVar("user_A",user_A+user_RT)
TNT1 A 0 A_Countdown
goto Spawn+3
Death:
TNT1 A 1
stop
}
}
actor HyperbombBlastFX_H2 : HyperbombBlastFX_H1
{
Mass 96
MeleeRange 26
reactiontime 12//15
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_RT",360/reactiontime)
TNT1 A 0 A_SetUserVar("user_A",random(0,user_RT))
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",Mass/accuracy,0,MeleeRange/accuracy,Mass/accuracy,0,MeleeRange/accuracy,user_A,37)
TNT1 A 0 A_SpawnItemEx("HyperbombBlastFX",Mass/accuracy,0,MeleeRange/-accuracy,Mass/accuracy,0,MeleeRange/-accuracy,user_A,37)
TNT1 A 0 A_SetUserVar("user_A",user_A+user_RT)
TNT1 A 0 A_Countdown
goto Spawn+3
Death:
TNT1 A 1
stop
}
}

actor HyperbombBlastFX_H3 : HyperbombBlastFX_H2
{
Mass 87
MeleeRange 50
reactiontime 10//12
}
actor HyperbombBlastFX_H4 : HyperbombBlastFX_H2
{
Mass 70
MeleeRange 70
reactiontime 8//9
}
actor HyperbombBlastFX_H5 : HyperbombBlastFX_H2
{
Mass 50
MeleeRange 87
reactiontime 6
}
actor HyperbombBlastFX_H6 : HyperbombBlastFX_H2
{
Mass 26
MeleeRange 96
reactiontime 4//3
}

actor HyperbombBlastFX : BasicClientSide
{
+FORCEXYBILLBOARD
+BRIGHT
renderstyle "Translucent"
States
{

Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
HARD DCB 1
ASEX ABBCD 1
ASEX EEFFF 1 A_Fadeout
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor S_HyperbombTrail : BasicClientside
{
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 2
MINB IIIIII 1
MINB IIIIII 1 A_Fadeout
stop
}
}