actor Ability1_Fireman : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "HyperBomb Ability"
weapon.ammotype1 "AbilityAmmo"
inventory.icon "BURNRI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
Weapon.SlotPriority 5
States
{

Ready:
FIRB B 0 
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
Ready1:
"####" A 0 A_FireCustomMissile("FireFieldAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady(WRF_NOSECONDARY)
goto Ready1

Fire:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" A 0 A_FireCustomMissile("FireFieldThrow",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 0 A_TakeInventory("AbilityCharge_1",1)
"####" A 0 
"####" A 0 A_GiveInventory("SignatureRecharge") //
"####" CDB 2
"####" A 1
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,110)
"####" A 0 A_Jump(256,"End")
goto End

AltFire:
"####" A 1
End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End
}
}
//====

actor UseAbility1_Fireman : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("FireFieldAbility",1)
fail
}
}

actor FireFieldAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_1",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop
}
}
//=================
//=================

actor FireFieldAimPath : GolfBallAimInstantStarter3 { Speed 45 Gravity 1}

Const Int IncArea = 150;
Const int FireField_EntiTID = 4;

actor FireFieldThrow : BasicThrowable
{
Speed 45
reactiontime 200
States
{

Spawn2:
MBAL B 1 A_SpawnItemEX("BasicThrowableTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
MBAL B 0 A_CountDown
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn.Bounced:
BOMB S 1 A_SpawnItemEX("FireFieldThrow2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Bounce.Floor:
BOMB S 1 A_SpawnItemEX("FireFieldArea",0,0,0,0,0,0,0,1,0)
stop

Death:
MINB A 0 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,1)
stop
}
}

actor FireFieldThrow2 : FireFieldThrow
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
MBAL B 1 A_CountDown
MBAL B 0 A_CheckFloor("Death")
loop
	
Death:
MINB A 0 A_SpawnItemEx("FireFieldArea",0,0,0,0,0,0,0,1)
stop
}
}

actor FireFieldArea : BasicExplosion2
{
DamageType "Molotov"
obituary "$OB_MOLOTOV"
ReactionTime 35
+OLDRADIUSDMG
Args 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
TNT1 D 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,FireField_EntiTID,2)
TNT1 A 5
TNT1 A 0 A_Explode(5,IncArea,0,0,IncArea)
Spawn2:
TNT1 A 1
TNT1 A 0 A_PlaySoundEx("weapon/WaveBurner","Weapon")
TNT1 AAA 0 A_SpawnItemEx("MolotovParticleSpawner",IncArea,0,-(z-floorz)+random(0,5),0,0,6,Random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 AAA 0 A_SpawnItemEx("MolotovParticleSpawner",Random(-IncArea,IncArea),0,-(z-floorz)+random(0,5),0,0,6,Random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetArg(0,Args[0]+1)
TNT1 A 0 A_JumpIf(Args[0] >= 7,"Spawn3")
Goto Spawn2
Spawn3:
TNT1 A 0 A_RadiusGive("MolotovOffGround",IncArea,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS)
TNT1 A 0 A_Explode(5,IncArea,0,0,IncArea)
TNT1 A 0 A_SetArg(0,0)
TNT1 A 0 A_CountDown
Goto Spawn2
Death:
TNT1 A 0 
stop
}
}


actor MolotovOffGround : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_CheckFloor("Nope")
TNT1 A 0 A_GiveInventory("MolotovNoGround",1)
stop

Nope:
TNT1 A 0
stop
}
}

actor MolotovNoGround : PowerProtection
{
Powerup.Duration 2
damagefactor "Molotov", 0.0
}

actor MolotovParticleSpawner : BasicHelper
{
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 1 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_SpawnItemEx("IncinerateParticleFX",0,0,-(z-floorz),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        stop
    }

}

actor IncinerateParticleFX : BasicClientside
{
//translation "192:192=218:218", "198:198=221:221"
Renderstyle "Normal" //"add"
scale 1.5
Alpha 0.7
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_SetScale(Frandom(1.0,2.0))
BRNF ABCDEFGH 1 A_ScaleVelocity(0.6)
stop
}
}