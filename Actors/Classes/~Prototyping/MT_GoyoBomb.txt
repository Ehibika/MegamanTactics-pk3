actor Ability2_Prototyper : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "RushCall Ability"
weapon.ammotype1 "AbilityAmmo"
inventory.icon "RCOIL"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
+WEAPON.NOAUTOFIRE
States
{

Ready:
XH0D A 0 
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
Ready1:
"####" A 0 A_FireCustomMissile("GoyoBombAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady
goto Ready1

Fire:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("GoyoBombThrow",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" D 1 A_TakeInventory("AbilityCharge_2",1)
"####" C 0 A_GiveInventory("RefireBuffer")
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("AbilityCharge_2",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" A 0 A_FireCustomMissile("GoyoBombAimPath2",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" A 1 A_Jumpif(CallACS("MT_ButtonCheck",BT_ATTACK)==1,"AltFire2")
"####" A 1 A_Refire
goto Ready1

AltFire2:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("GoyoBombUnderthrow",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" D 1 A_TakeInventory("AbilityCharge_2",1)
"####" C 0 A_GiveInventory("RefireBuffer")
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("AbilityCharge_2",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

Fire.Readyup:
XH0D A 2 Offset(-4,69)
XH0D A 2 Offset(-2,48)
goto Ready1

End:
"####" M 1 A_TakeInventory("AbilityActiveFlag",9)
"####" M 0 
Goto End
}
}
//====

Actor GoyoBombFirehitDelay : powerprotection
{
Powerup.duration 8
Damagefactor "GoyoBombFire", 0
}

actor UseAbility2_Prototyper : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("GoyoBombAbility",1)
fail
}
}

actor GoyoBombAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_2",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================

actor GoyoBombAimPath : GolfBallAimInstantStarter { Speed 64 Gravity 1}
actor GoyoBombAimPath2 : GolfBallAimInstantStarter { Speed 25 Gravity 1}

actor GoyoBombthrow : BasicThrowable
{
Obituary "$OB_GOYOBOMB"
radius 6
height 24
scale 1.0
States
{
Spawn:
TNT1 A 0
OILP A 0 A_GiveInventory("ProjectileCode",1)
OILP A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
OILP A 1 A_SpawnItemEX("BasicThrowableTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
OILP A 0 A_CountDown
OILP A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
Bounce:
Death:
MINB A 0 A_SpawnItemEx("GoyoBomb",0,0,0,0,0,0,0,1)
stop
}
}

actor GoyoBombUnderThrow : GoyoBombthrow { Speed 25 gravity 1.0}


actor GoyoBomb  
{
+MISSILE
+NOBLOOD
+DONTBLAST
+SEEKERMISSILE
+NOTARGETSWITCH
+SHOOTABLE
+NOGRAVITY
+NOCLIP
+LOOKALLAROUND
Obituary "$OB_DYNAMITE"
DamageType "Dynamite"
scale 1.0
radius 6
height 32
speed 0
health 10
args 0,8
states
{
Spawn:
TNT1 A 0
TNT1 A 0 //A_CheckCeiling("SpawnCeiling")
goto Spawn1

SpawnCeiling:
MXP1 A 0 A_SetScale(Scalex,-ScaleY)
MXP1 A 0 A_SetArg(0,-args[1])
OILP A 0 A_GiveInventory("ProjectileCode",1)
OILP A 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,4,3)
goto Spawn1

Spawn1:
OILP A 1
Goto Spawn1

Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_PlaySoundEx("misc/MetDie","Weapon")
TNT1 A 0 A_SpawnItemEx("ExplosionEffect2",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("GoyoBombDropper",0,0,0,0,0,-1,0,1|SXF_MULTIPLYSPEED)
TNT1 A 3
stop
}
}

Actor GoyoBomb_B : GoyoBomb{Designatedteam 0 Species "LightMember" }
Actor GoyoBomb_R : GoyoBomb{Designatedteam 1 Species "WilyMember" }
Actor GoyoBomb_G : GoyoBomb{Designatedteam 2 Species "CossackMember" }
Actor GoyoBomb_O : GoyoBomb{Designatedteam 3 Species "KingMember" }

actor GoyoBombDropper
{
PROJECTILE
-NOGRAVITY
height 4
radius 4
Speed 25
gravity 1.0
Scale 1.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 A_SpawnItemEx("MeltCreeperShotTrail",0,0,0,0,0,0,0,1)
FWVE G 1 
loop
Death:
TNT1 A 0 A_SpawnItemEx("GoyoBombExplosion",0,0,-(z-floorz),0,0,0,0,1)
TNT1 AAAAA 2 A_SpawnItemEx("MeltCreepererFx",random(-5,5),random(-5,5),random(-5,5),0,0,4,0,1)
stop
}
}

actor GoyoBombExplosion : BasicExplosion
{
Obituary "$OB_GOYOBOMB"
Scale 5.5
States
{
Spawn:
NAPC A 0 
MBAL B  0 A_GiveInventory("ProjectileCode",1)
SNAK A 0 A_SpawnItemEx("GoyoBombFireSpawner", 0,0,0, 1, 0, 0, Random(-15, 15), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SNAK A 0 A_SpawnItemEx("GoyoBombFireSpawner", 0,0,0, 1, 0, 0, Random(75, 105), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SNAK A 0 A_SpawnItemEx("GoyoBombFireSpawner", 0,0,0, 1, 0, 0, Random(165, 195), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SNAK A 0 A_SpawnItemEx("GoyoBombFireSpawner", 0,0,0, 1, 0, 0, Random(-75, -105), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
TNT1 A 35 A_SpawnItemEx("ExplosionEffect2")
stop
}
}


actor GoyoBombFireSpawner
{
PROJECTILE
+RIPPER
+DONTREFLECT
+THRUGHOST
+ACTIVATEMCROSS
//+FLOORHUGGER
+NOEXPLODEFLOOR
+SLIDESONWALLS
Radius 5
Height 10
scale 2.5
speed 16
damage 0
damagetype "GoyoBombFire"
Obituary "$OB_GOYOBOMB"
maxstepheight 33
-NOGRAVITY
+DONTBLAST
+NOBLOOD
painchance 0
reactiontime 2
renderstyle none
States
{
Spawn:
SWHE A 0
SWHE A 0 A_SpawnItemEx("GoyoBombFireSpawner2", 0,0,0, 1, 0, 0, 90, SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SWHE A 0 A_SpawnItemEx("GoyoBombFireSpawner2", 0,0,0, 1, 0, 0, 270, SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SWHE A 10
SWHE F 0 A_CountDown
Goto Spawn
Death:
TNT1 A 0 
stop
}
}

actor GoyoBombFireSpawner2 : GoyoBombFireSpawner
{
ReactionTime 2
States
{
Spawn:
SWHE A 0
SWHE ABCDEF 2 A_SpawnItemEx("GoyoBombFire", 0, 0, 0, Random(-5, 5),Random(-5, 5),0,0,SXF_TRANSFERTRANSLATION)
SWHE F 0 A_CountDown
Goto Spawn
Death:
TNT1 A 0 
stop
}
}

actor GoyoBombFire : GoyoBombFireSpawner
{
-FLOORHUGGER
//-RIPPER
+BRIGHT
Damage(5)
seesound "weapon/firestorm"
scale 2.5
renderstyle normal
reactiontime 70
States
{
Spawn:
BRNF A 0
BRNF A 0 A_PlaySoundEx("weapon/WaveBurner","Weapon")
Spawn2:
BRNF AB 2 
BRNF A 0 A_Stop
BRNF A 0 A_CountDown
Goto Spawn2
Death:
FLMB A 0 A_SpawnItemEx("GoyoBombFireFading",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_CLIENTSIDE)
stop
}
}

actor GoyoBombFireFading : GoyoBombFire
{
+NOINTERACTION
+CLIENTSIDEONLY
States
{
Spawn:
BRNF AB 2 A_FadeOut(0.05)
Loop
}
}