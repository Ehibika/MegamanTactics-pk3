


actor ElectriggerVWep : WepMasterBase
{
Tag "ElectriggerV"
Weapon.AmmoUse 1
Weapon.AmmoGive 45
scale 2.0
Obituary "$OB_ELECTRIGGERV"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "ElectriggerV_Ammo"
inventory.icon "VLTSI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
DW32 B 1 A_WeaponReady(WRF_ALLOWINPUTS)
"####" B 0 A_Jumpifinventory("ElectriggerV_Ammo",1,"Ready.Core")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,0.9*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_GunFlash("Flash.Sound")

hold:
"####" B 0
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_Jumpifinventory("ConShotTimer",1,"hold.Effect")
"####" B 0 A_Jump(256,"hold.Bullet")
Goto hold.Bullet
hold.Bullet:
"####" B 0 A_FireCustomMissile("ElectriggerVBurst",0,1,0,0)
"####" B 0 A_FireCustomMissile("ElectriggerVDamager",0,0,0,0)
"####" B 0 A_GiveInventory("ElectriggerVCST",1)
"####" C 2 A_WeaponReady(WRF_FIRING)
"####" B 0 A_Refire("hold")
"####" B 1 A_WeaponReady(WRF_FIRING)
goto Ready.Wep
hold.Effect:
"####" B 0 A_FireCustomMissile("ElectriggerVBurst",0,0,0,0)
"####" C 2 A_WeaponReady(WRF_FIRING)
"####" B 0 A_Refire("hold")
"####" B 1 A_WeaponReady(WRF_FIRING)
goto Ready.Wep


NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("ElectriggerV_Ammo",1,"Ready")
Goto Reload

Reload.Hold:
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 35 A_WeaponReady(WRF_RELOADING)
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
goto Reload.Rise

Flash.Sound:
TNT1 A 3 A_PlaySoundEx("weapon/electricshock","Weapon")
TNT1 A 0 A_Jumpifinventory("WeaponCharge",25,"Flash.Sound")
Stop

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor ElectriggerVWep2 : ElectriggerVWep
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor ElectriggerV_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 45
Ammo.BackpackMaxAmount 60
+INVENTORY.IGNORESKILL
}

Actor ElectriggerVhitDelay : powerprotection
{
Powerup.duration 3
Damagefactor "ElectriggerV", 0
}

actor ElectriggerVCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 4
powerup.type "ConShotTimer"
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const Int ElectriggerVRange = SprayerRange+55;
Const float ElectriggerV_DMG = 6;
Const float ElectriggerV_RAD = 64;

actor ElectriggerVDamager : FastProjectileBase
{
    PROJECTILE
    damagetype "ElectriggerV"
    Obituary "$OB_ELECTRIGGERV"
    +SKYEXPLODE
    +EXPLODEONWATER
    +HITTRACER
    RenderStyle None
    Damage (ElectriggerV_DMG) 
    speed 1200 //15 * ElectriggerVRange(80)
    scale 2.5
    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1 A_Stop
        TNT1 A 0
        stop
    Crash:
    TNT1 A 1 A_SpawnItemEx("ElectriggerVChainer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
    XDeath:
    Death:
    TNT1 A 0
    stop
    }
}


actor ElectriggerVChainer : BasicHelper
{
+SEEKERMISSILE
reactionTime 5
    states
    {
    Spawn:
    TNT1 A 0
    TNT1 A 0 A_Jumpifinventory("Pro_ElecResistance",1,"Spawn.Fail",AAPTR_TRACER)
    TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TRACER, AAPTR_NULL)
    Spawn2:
    TNT1 A 0 A_SeekerMissile (90, 90, SMF_LOOK, 256, 4)
    TNT1 A 0 A_Warp(AAPTR_DEFAULT, 0, 0, 0, 0, WARPF_STOP)
    TNT1 A 0 A_JumpIf(CallACS("core_compareptr", AAPTR_TRACER, AAPTR_MASTER), "Spawn.Retry")
    TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER,AAPTR_NULL) == false, "Chain")
    TRET A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_NULL)  
    TRET A 1 A_CountDown
    goto Spawn2
    Spawn.Retry:
    TNT1 A 0 A_GiveInventory("ElectriggerVCantseek", 1, AAPTR_TRACER)
    TRET A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_NULL)  
    TRET A 1 A_CountDown
    goto Spawn2

    Chain:
    TNT1 A 0 A_GiveInventory("ElectriggerVCantseek", 1, AAPTR_TRACER)
    TNT1 A 0 ACS_NamedExecuteWithResult("core_lookatptr", AAPTR_TRACER)
    TNT1 A 0 A_Warp(AAPTR_MASTER,48,0,32,0,16+8+WARPF_USECALLERANGLE)
    TRET A 0 A_SpawnItemEx("ElectriggerVDamager", 0,0,0, cos(pitch)*1, 0, sin(-pitch)*1, 0, SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
    TRET A 0 A_SpawnItemEx("ElectriggerVBurst", 0,0,0, cos(pitch)*1, 0, sin(-pitch)*1, 0, SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
    stop

    Spawn.Fail:
    TRET A 0
    Stop
    }
}

actor ElectriggerVCantseek : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    States
    {
    Use:
        TNT1 A 0 A_Jumpifinventory("PlayerPropertyCantseek",1,"Nope")
        TNT1 A 0 ACS_NamedExecuteWithResult("core_cantseekplayer", 0, true)
        TNT1 A 0 A_GiveInventory("SparkChaserCantSeekCounter", 6)
        TNT1 A 0 A_SpawnItemEx("ElectriggerVCantseekWatcher")
        TNT1 A 0 //A_log("noseeking")
        stop
    Nope: 
    TNT1 A 0 //A_log("Still noseeking")
    stop
    }
}

actor ElectriggerVCantseekWatcher
{
    PROJECTILE
    +NOINTERACTION
    renderstyle none
    States
    {
    Spawn:
        TNT1 A 0  
        TNT1 A 1 
        TNT1 A 0 A_TakeFromTarget("SparkChaserCantSeekCounter", 1)
        TNT1 A 0 A_JumpIfInTargetInventory("SparkChaserCantSeekCounter", 1, "Spawn")
        TNT1 A 0 A_GiveToTarget("ElectriggerVCanSeek", 1)
        //TNT1 A 0 A_log("CanSeekNow")
        stop
    }
}

actor ElectriggerVCanSeek : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    States
    {
    Use:
        TNT1 A 0 ACS_NamedExecuteWithResult("core_cantseekplayer", 0, false)
        stop
    }
}


actor ElectriggerVBurstStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("ElectriggerVBurst",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor ElectriggerVBurst : FastProjectileBase//FastProj to match damager hitbox
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > ElectriggerVRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("ElectriggerVBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("ElectriggerVFX",0,frandom(-3,3),frandom(-3,3),0,0,0,0,1)
        TNT1 A 0 A_SpawnItemEx("ElectriggerVBurst2",0,frandom(-6,6),frandom(-6,6),0,0,0,0,1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
        TNT1 A 0 A_SpawnItemEx("ElectriggerVEndFX",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
        TNT1 A 0 A_SpawnItemEx("ElectriggerVEndFX",0,0,0,0,0,0,0,1)
        stop
    }
}

actor ElectriggerVFX : BasicGraphicEffect
{
    +FORCEXYBILLBOARD
    +BRIGHT
    renderstyle "ADD"
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 acs_NamedExecuteWithResult("SprayerAlpha",0.4*FVM)
        ESPA Q 0
        ESPA Q 0 A_Jump(256,1,4)
        ESPA QRS 1
        stop
        ESPA XWV 1
        stop
    }
}

actor ElectriggerVEndFX : ElectriggerVFX
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 acs_NamedExecuteWithResult("SprayerAlpha",0.8*FVM)
        ESPA ZYZY 1
        ESPA Z 0 A_SetScale(1.25)
        ESPA ZY 1
        stop
    }
}

actor ElectriggerVBurst2
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    +BRIGHT
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(random(1,4) >= 4,"SpawnReal")
        stop
    SpawnReal:
        TNT1 A 10 A_SetTics(random(2,4)*3)
        TNT1 A 0 A_PlaySoundEx("weapon/electricshockspark","Auto")
        TNT1 A 0 A_SetPitch(random(12,18))
        TNT1 A 0 A_SpawnItemEx("ElectriggerVBurst3",0,0,0,0,0,0,0,544+1)//512+32
        stop
    Death:
        TNT1 A 0
        stop
    }
}

actor ElectriggerVBurst3 : ElectriggerVBurst2
{
    +BRIGHT
    Scale 0.5
    States
    {
    Spawn:
        ESPA T 0
        ESPA T 0 A_JumpIf(Pitch < 2,"Death")
        ESPA T 0 A_SetPitch(Pitch-1)
        ESPA T 0 A_SpawnItemEx("ElectriggerVBurst3",-2*random(0,1)+1, -2*random(0,1)+1, -2*random(0,1)+1,0,0,0,0,544+1)//512+32
        TNT1 A 0 acs_NamedExecuteWithResult("SprayerAlpha",0.4*FVM)
        ESPA T 0 A_SetTics(random(0,2))
        ESPA T 0 A_Jump(256,1,2)
        ESPA T 1
        stop
        ESPA U 1
        stop
    }
}

