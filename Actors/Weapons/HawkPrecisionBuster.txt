/*=============================

*///===========================

actor HawkPrecisionBusterWep : WepMasterBase
{
Tag "Hawk Precision Buster"
Weapon.AmmoUse 35
Weapon.AmmoGive 0

Obituary "$OB_HAWKPRECISIONBUSTER"
inventory.icon "HBEAI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "HawkPrecisionBuster_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop
Ready:
Ready.Wep:
DW34 A 1 A_WeaponReady(WRF_ALLOWINPUTS)
//"####" A 0 A_Jumpifinventory("HawkPrecisionBuster_Ammo",1,"Ready.Core")
Goto Ready.Wep

//===============
Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
"####" A 0 A_ZoomFactor(1.0,0)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,0.8*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop

Fire:
"####" C 0 A_Jumpifinventory("isBot",1,"BotRefire")
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"NoAmmo")
"####" A 0 A_Jumpifinventory("HawkPrecisionBusterTimer",1,"NoAmmo")
"####" A 0 A_Jumpifinventory("RefireBuffer",1,"Hold")
"####" A 0 A_Jumpifinventory("SemiAutoBuffer",1,"Hold")

"####" A 0 //A_ZoomFactor(3.5,0)
Hold:
"####" A 0 A_Giveinventory("HawkPrecisionBuster_Ammo",1)
"####" A 0 A_Giveinventory("HawkPrecisionBusterFSPD")
"####" C 0 A_Jumpifinventory("HawkPrecisionBuster_Ammo",35,"Hold.Charged")
"####" A 1 A_Jumpif(CallACS("MT_ButtonCheck",BT_ALTATTACK)==1,"Cancel")
"####" C 0 A_Refire
goto PreRelease

Hold.Charged:
"####" B 0 ACS_NamedExecuteAlways("MT_LocalSounds",0,1)
Hold.Charged2:
"####" A 0 A_Giveinventory("HawkPrecisionBusterFSPD")
"####" A 1 A_Jumpif(CallACS("MT_ButtonCheck",BT_ALTATTACK)==1,"Cancel")
"####" C 0 A_Jumpifinventory("isBot",1,"Release")
"####" C 0 A_Refire("Hold.Charged2")
goto Release

PreRelease:
"####" A 0 A_Giveinventory("HawkPrecisionBusterFSPD")
"####" A 0 A_Takeinventory("HawkPrecisionBuster_Ammo",1)
"####" AA 1 A_Jumpif(CallACS("MT_ButtonCheck",BT_ALTATTACK)==1,"Cancel")
"####" C 0 A_Refire("Hold")
"####" C 0 A_Jumpifinventory("HawkPrecisionBuster_Ammo",1,"PreRelease")
goto Cancel

Release:
"####" B 0 A_PlaySoundEx("weapon/HardBuster6","Weapon")
"####" B 0 A_PlaySoundEx("weapon/coilexplode", "SoundSlot6")
"####" B 0 A_FireCustomMissile("HawkPrecisionBusterStart",0,0,0,0)
"####" B 0 A_FireCustomMissile("HawkPrecisionBusterDamager",0,1,0,0)
"####" A 0 A_Giveinventory("HawkPrecisionBusterCST")
"####" BC 2
"####" A 17
"####" C 0 A_ClearRefire
"####" A 0 A_Takeinventory("HawkPrecisionBusterFSPD")
"####" A 0 //A_ZoomFactor(1.0,0)
goto Ready.Wep

Cancel:
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_Takeinventory("HawkPrecisionBuster_Ammo",999)
"####" A 0 A_Takeinventory("HawkPrecisionBusterFSPD")
"####" A 5 //A_ZoomFactor(1.0,0)
CancelHold:
"####" A 1 
"####" C 0 A_Refire("CancelHold")
goto Ready.Wep
/*
Fire:
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" C 0 A_Jumpifinventory("isBot",1,"BotRefire")
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"NoAmmo")
"####" A 0 A_Jumpifinventory("HawkPrecisionBusterTimer",1,"NoAmmo")
"####" A 0 A_Jumpifinventory("RefireBuffer",1,"Hold")
"####" A 0 A_Jumpifinventory("SemiAutoBuffer",1,"Hold")
"####" "#" 0 A_PlaySoundEx("weapon/HardBuster6","Weapon")
"####" "#" 0 A_PlaySoundEx("weapon/coilexplode", "SoundSlot6")

"####" B 0 A_FireCustomMissile("HawkPrecisionBusterStart",0,0,0,0)
"####" B 0 A_FireCustomMissile("HawkPrecisionBusterDamager",0,1,0,0)
"####" A 0 A_Giveinventory("HawkPrecisionBusterCST")
"####" A 0 A_JumpIfNoAmmo(2)
"####" A 0 A_Giveinventory("HPC_DelayStart")
"####" A 0
"####" BC 2
Hold:
"####" A 1 A_WeaponReady(WRF_ALLOWONLYINPUTS)
"####" C 0 A_Jumpifinventory("isBot",1,"BotRefire")
"####" C 0 A_Refire
goto Ready.Wep
*/
Reload:
"####" A 0 A_Jumpifinventory("ZoomMode",1,"Reload.DeZoom")

"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,0.5*FVM)
"####" A 0 A_ZoomFactor(3.5,0)
"####" A 0 A_GiveInventory("ZoomMode",1)
"####" A 7
goto Ready
Reload.DeZoom:
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,0.8*FVM)
"####" A 0 A_ZoomFactor(1.0,0)
"####" A 0 A_TakeInventory("ZoomMode",9)
"####" A 7
goto Ready

NoAmmo:
"####" A 1
//"####" A 0 A_Jumpifinventory("HawkPrecisionBuster_Ammo",1,"Ready")
Goto Ready

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor HawkPrecisionBusterWep2 : HawkPrecisionBusterWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor HawkPrecisionBuster_Ammo : Ammo //Used as charge
{
inventory.amount 1
inventory.maxamount 35
Ammo.BackpackMaxAmount 35
}

actor ZoomMode : Inventory {inventory.amount 1 inventory.maxamount 2}

actor HawkPrecisionBusterCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 12
powerup.type "ConShotTimer"
}

actor HawkPrecisionBusterFSPD : FiringSpeed
{
Powerup.Duration 25
speed 0.4
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const Int HawkPrecisionBusterRange = 400;

actor HawkPrecisionBusterDamager : FastProjectileBase
{
    PROJECTILE
    damagetype "HawkPrecisionBuster"
    Obituary "$OB_HAWKPRECISIONBUSTER"
    +SKYEXPLODE
    +EXPLODEONWATER
    +HITTRACER
    RenderStyle None
    Damage (100)
    speed 3000 //15 * HawkPrecisionBusterRange(200)
    scale 2.5
    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1 
        stop
    
    Death:
    TNT1 A 1 
    stop
    }
}

actor HawkPrecisionBusterStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,15,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("HawkPrecisionBusterRay",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor HawkPrecisionBusterRay : FastProjectilebase//FastProj to match damager hitbox
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    RenderStyle None
    Damage (0)
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > HawkPrecisionBusterRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("HawkPrecisionBusterRay",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("HawkPrecisionBusterRayFX",0,0,0,0,0,0,0,1)
        TNT1 A 0 A_SpawnItemEx("ElectriggerVFX",0,frandom(-3,3),frandom(-3,3),0,0,0,0,1)
        TNT1 A 0 A_SpawnItemEx("ElectriggerVBurst2",0,frandom(-6,6),frandom(-6,6),0,0,0,0,1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 A_SpawnItemEx("HawkPrecisionBusterPuff",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("HawkPrecisionBusterPuff",0,0,0,0,0,0,0,1)
        stop
    }
}

actor HawkPrecisionBusterTimer : Inventory
{
inventory.amount 1
inventory.maxamount 52
}

actor HPC_DelayStart : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("HawkPrecisionBusterTimer",52)
TNT1 A 0 A_Spawnitemex("HawkPrecisionBusterReadyWatcher")
stop
Nope:
TNT1 A 0
stop
}
}

actor HPC_DelayEnd : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_PlaySoundEX("weapon/Trigger","Soundslot6")
stop
Nope:
TNT1 A 0
stop
}
}

actor HawkPrecisionBusterReadyWatcher : WatcherBase 
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("HawkPrecisionBusterTimer",1)
TNT1 A 0 A_JumpIfInTargetInventory("HawkPrecisionBusterTimer",1,"Spawn")
TNT1 A 0 A_GiveToTarget("HPC_DelayEnd",1)
Death:
DeathTake:
TNT1 A 0
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Effect Actors|-------------------------------
//[+]========================================================================[+]
/*
actor HawkPrecisionBusterPuff : FXActor_A
{
scale 1.5
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}
*/

actor HawkPrecisionBusterPuff : BasicClientside//BulletPuff
{
scale 2.5
DamageType "HawkPrecisionBuster"
Obituary "$OB_HAWKPRECISIONBUSTER"
Renderstyle "add"
alpha 1.0
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileFXCode",1)
TNT1 A 0 A_PlaySoundEx("weapon/LightningBolt","Weapon")
ESPA ZYZY 1 A_SpawnItemEx("ElectriggerVFX",0,frandom(-36,36),frandom(-36,36),0,0,0,0,1)
TNT1 AAAAA 0 A_SpawnItemEx("ElectriggerVBurst2",0,frandom(-36,36),frandom(-36,36),0,0,0,0,1)
stop
}
}

actor HawkPrecisionBusterRayFX : BasicClientside
{
scale 2.0
renderstyle "add"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
SHOC CD 1
SHOC CDCDCDCD 1 A_Fadeout
stop
}
}