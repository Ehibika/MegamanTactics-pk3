actor Dynamite : BaseMM8BDMUseItem
{
inventory.amount 1
inventory.maxamount 3
inventory.pickupmessage "$PU_Dynamite"
Tag "$TAG_Dynamite"
inventory.icon "REMOI"
Inventory.PickupSound "item/1up"
+INVBAR
+COUNTITEM
scale 2.0
States
{
Use:
//EBAL E 0 A_PlaySoundEx("item/refill","Voice")
TNT1 A 0 ACS_NamedExecuteAlways("RunItemAbility",0,4)
fail
}
}

actor ItemAbility_4 : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "SmokeGrenade Ability"
weapon.ammotype1 "AbilityAmmo"
inventory.icon "REMOI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
XH0D A 0 
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
Ready1:
"####" A 0 A_FireCustomMissile("DynamiteAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady
goto Ready1

Fire:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("DynamiteThrow",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" E 0 A_TakeInventory("Dynamite",1)
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("Dynamite",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" A 0 A_FireCustomMissile("DynamiteAimPath2",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" A 1 A_Jumpif(CallACS("MT_ButtonCheck",BT_ATTACK)==1,"AltFire2")
"####" A 1 A_Refire
goto Ready1

AltFire2:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("DynamiteUnderthrow",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" E 0 A_TakeInventory("Dynamite",1)
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("Dynamite",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

Fire.Readyup:
XH0D A 2 Offset(-4,69)
XH0D A 2 Offset(-2,48)
goto Ready1


End:
"####" C 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor DynamiteAimPath : GolfBallAimInstantStarter { Speed 35 Gravity 1}
actor DynamiteAimPath2 : GolfBallAimInstantStarter { Speed 25 Gravity 1}


Const int Dynamite_DMG = 80;
Const int Dynamite_RAD = 360; //Sensor radius
Const int Dynamite_RAD2 = 210; //Damge radius
Const int Dynamite_RADMin = 96;

actor DynamiteThrow  
{
+MISSILE
+NOTARGETSWITCH
+NOBLOOD
+DONTBLAST
+SKYEXPLODE
+ThruActors
Obituary "$OB_DYNAMITE"
DamageType "Dynamite"
radius 8
height 8
scale 2.5
Speed 35 
gravity 1.0
states
{
Spawn:
DMIT A 0
DMIT A 0 A_GiveInventory("ProjectileCode",1)
DMIT A 3
goto spawn+2
Death:
DMIT A 0 A_GiveInventory("SpawnDynamiteItem",1)
stop
}
}

actor DynamiteUnderThrow : Dynamitethrow { Speed 25 gravity 1.0}



actor DynamiteBomb  
{
+MISSILE
+NOBLOOD
+DONTBLAST
+SEEKERMISSILE
+NOTARGETSWITCH
+SHOOTABLE
+NOGRAVITY
+NOCLIP
+LOOKALLAROUND
Obituary "$OB_DYNAMITE"
DamageType "Dynamite"
scale 2.5
radius 8
height 8
speed 0
health 10
scale 2
args 0,8
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_CheckCeiling("SpawnCeiling")
goto Spawn1

SpawnCeiling:
MXP1 A 0 A_SetScale(Scalex,-ScaleY)
MXP1 A 0 A_SetArg(0,-args[1])
goto Spawn1

Spawn1:
DMIT A 0 A_GiveInventory("ProjectileCode",1)
DMIT A 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,4,3)
DMIT A 1 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT)
DMIT A 30
DMIT A 1 A_PlaySoundEX("weapon/flashstopper","Weapon")
DMIT ABABAB 3 //A_FadeTo(0.3)
DMIT A 0 
DMIT C 0 A_Jump(256,"Scan")
Goto Scan

Scan:
DMIT A 0 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
Scan1:
DMIT A 0 A_SetArg(0,Args[0]+1)
DMIT A 4 A_Jumpif(Args[0]>5,"Scan2")
DMIT A 1  A_LookEx(LOF_NOSOUNDCHECK, 1, Dynamite_RAD, Dynamite_RAD, 360, "Targetcheck")
Goto Scan1
Scan2: //Blink
DMIT A 0 A_SetArg(0,0)
DMIT A 4 //A_SpawnItemEx("SLBombPulseFX",0,0,args[1],0,0,0,0,1)
DMIT A 1  A_LookEx(LOF_NOSOUNDCHECK, 1, Dynamite_RAD, Dynamite_RAD, 360, "Targetcheck")
Goto Scan1
Targetcheck:
DMIT A 1 A_Jumpifcloser(Dynamite_RAD,"Targetcheck2")
Goto Scan
Targetcheck2:
DMIT A 1 A_Jumpif(CallACS("TrapSensorIdentify")==1,"Scan") //You Are A Teammate
Goto Explode

Explode:
DMIT B 0 A_PlaySoundEX("weapon/BombAlarm","Soundslot6")
DMIT ABABA 5
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
TNT1 A 0 A_SpawnItemEx("ExplosionEffect2",0,0,0,0,0,0,0,1)
TNT1 A 0 A_PlaySoundEx("weapon/Napalm","Weapon")
TNT1 A 0 A_Explode(Dynamite_DMG,Dynamite_RAD2,1,0,Dynamite_RADMin)
stop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_PlaySoundEx("misc/MetDie","Weapon")
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,1)
TNT1 A 3
stop
}
}

Actor DynamiteBomb_B : DynamiteBomb{Designatedteam 0 Species "LightMember" }
Actor DynamiteBomb_R : DynamiteBomb{Designatedteam 1 Species "WilyMember" }
Actor DynamiteBomb_G : DynamiteBomb{Designatedteam 2 Species "CossackMember" }
Actor DynamiteBomb_O : DynamiteBomb{Designatedteam 3 Species "KingMember" }


actor SpawnDynamiteItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SetPitch(z-floorZ)

TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")

TNT1 A 0 A_SpawnItemEx("DynamiteBomb",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//SXF_TRANSFERPOINTERS
stop
BFire:
TNT1 A 0 A_SpawnItemEx("DynamiteBomb_B",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//
stop
RFire:
TNT1 A 0 A_SpawnItemEx("DynamiteBomb_R",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
GFire:
TNT1 A 0 A_SpawnItemEx("DynamiteBomb_G",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
YFire:
TNT1 A 0 A_SpawnItemEx("DynamiteBomb_O",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
}
}