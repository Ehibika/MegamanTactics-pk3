actor MiniSmoke : BaseMM8BDMUseItem
{
inventory.amount 1
inventory.maxamount 3
inventory.pickupmessage "$PU_MiniSmoke"
Tag "$TAG_MiniSmoke"
inventory.icon "SMKBM"
Inventory.PickupSound "item/1up"
+INVBAR
+COUNTITEM
scale 2.0
States
{
Use:
//EBAL E 0 A_PlaySoundEx("item/refill","Voice")
TNT1 A 0 ACS_NamedExecuteAlways("RunItemAbility",0,1)
fail
}
}

actor ItemAbility_1 : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "SmokeGrenade Ability"
weapon.ammotype1 "AbilityAmmo"
inventory.icon "SMKBM"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
XH0D A 0 
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
Ready1:
"####" B 0 A_FireCustomMissile("BasicThrowableAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady
goto Ready1

Fire:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("BasicSmokeBall",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" E 0 A_TakeInventory("MiniSmoke",1)
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("MiniSmoke",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 0 A_FireCustomMissile("BasicThrowableAimPath2",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" A 1 A_Jumpif(CallACS("MT_ButtonCheck",BT_ATTACK)==1,"AltFire2")
"####" A 1 A_Refire
goto Ready1

AltFire2:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("BasicSmokeBallUnderThrow",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" E 0 A_TakeInventory("MiniSmoke",1)
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("MiniSmoke",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

Fire.Readyup:
XH0D A 2 Offset(-4,69)
XH0D A 2 Offset(-2,48)
goto Ready1


End:
"####" C 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====


Const int SmkScrM_EntiTID = 4;

actor BasicSmokeBall : BasicThrowable
{
States
{

Spawn2:
MBAL B 1 A_SpawnItemEX("BasicThrowableTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
MBAL B 0 A_CountDown
MBAL B  1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn.Bounced:
MBAL B 1 A_SpawnItemEX("BasicSmokeBall2",0,0,0,momx*0.1,momy*0.1,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Bounce.Floor:
MINB A 0 A_PlaySoundEx("weapon/Shad_Poof","Weapon")
MBAL B 1 A_SpawnItemEX("SmokeScreenField",0,0,0,0,0,0,0,1,0)
stop

Death:
MINB A 0 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,1)
stop
}
}

actor BasicSmokeBall2 : BasicSmokeBall
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
MBAL B  0 A_GiveInventory("ProjectileCode",1)
MBAL B  1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
MBAL B 1 A_CountDown
MBAL B 0 A_CheckFloor("Death")
loop
	
Death:
MINB A 0 A_PlaySoundEx("weapon/Shad_Poof","Weapon")
MINB A 0 A_SpawnItemEx("SmokeScreenField",0,0,0,0,0,0,0,1)
stop
}
}

actor BasicSmokeBallUnderThrow : BasicSmokeBall {Speed 25}

actor SmokeScreenField : BasicHelper
{
ReactionTime 35
states
{
Spawn:
TNT1 A 0
TNT1 D 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,SmkScrM_EntiTID,3)
//The script above allows for a set number of actors that share this EntiTID, if more are spawned, the earliest one
//is removed. this keeps you limited to only 3 smokes on the field.
TNT1 A 2 A_GiveInventory("ProjectileCodeSyncTarget",1)
TNT1 A 0 A_SpawnItemEx("SmokeScreenRing",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS,0,tid)
TNT1 AAA 5
Goto Spawnloop

SpawnLoop:
TNT1 A 0 ACS_NamedExecuteAlways("MT_RadiusGive",0,7,175,RG_ENEMY|RG_SELF|RG_TEAM)
TNT1 A 5 A_CountDown
Loop 

Death:
TNT1 A 1 
stop
}
}


actor SmokeScreenRing //: BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 3
    var int user_angle;
    PROJECTILE
    +NOINTERACTION
    +NOCLIP
    //+CLIENTSIDEONLY
    +NOTARGETSWITCH
	States
	{
		Spawn:
		TNT1 A 0
        TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 1 A_SpawnItemEx("SmokeScreenCloud",0,0,64,0,0,0,0,1061,0,tid)
        SpawnLoop:
        TNT1 A 0 A_SpawnItemEx("SmokeScreenCloud",96,0,0,0,0,0,user_angle,1061|SXF_TRANSFERPOINTERS,0,tid)
        TNT1 A 0 A_SpawnItemEx("SmokeScreenCloud",-96,0,0,0,0,0,user_angle,1061|SXF_TRANSFERPOINTERS,0,tid)
		TNT1 A 1 A_SetUserVar("user_angle", user_angle + 60)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor SmokeScreenCloud
{
    +MISSILE
    +NOGRAVITY
    +NOINTERACTION
    +NOCLIP
    +FORCEXYBILLBOARD
    radius 8
    height 8
    Damage (0)
    scale 3.0
    Reactiontime 35
    Renderstyle "Translucent"
    Alpha 0.9
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
SMKK ABC 5
Spawn2:
SMKK DE 5 A_CountDown
loop
Death:
SMKK FGH 3 A_Fadeout(0.2)
stop
}
}


actor RadiusGiveItem_7 : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("BlindGuard")
stop
}
}