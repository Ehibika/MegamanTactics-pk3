actor MiniMolly : BaseMM8BDMUseItem
{
inventory.amount 1
inventory.maxamount 3
inventory.pickupmessage "$PU_MiniMolly"
Tag "$TAG_MiniMolly"
inventory.icon "FLMBSI"
Inventory.PickupSound "item/1up"
+INVBAR
+COUNTITEM
scale 2.0
States
{
Use:
//EBAL E 0 A_PlaySoundEx("item/refill","Voice")
TNT1 A 0 ACS_NamedExecuteAlways("RunItemAbility",0,9)
fail
}
}

actor ItemAbility_9 : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MINIMOLLY"
Tag "SmokeGrenade Ability"
weapon.ammotype1 "AbilityAmmo"
inventory.icon "FLMBSI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
XH0D A 0 
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
Ready1:
"####" A 0 A_FireCustomMissile("BasicThrowableAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady
goto Ready1

Fire:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("MiniMollythrow",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" E 0 A_TakeInventory("MiniMolly",1)
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("MiniMolly",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
XH0D A 0 Offset(0,48)
"####" A 0 A_FireCustomMissile("BasicThrowableAimPath2",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" A 1 A_Jumpif(CallACS("MT_ButtonCheck",BT_ATTACK)==1,"AltFire2")
"####" A 1 A_Refire
goto Ready1

AltFire2:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("MiniMollyUnderthrow",0,1,0,-20,0,Aim_ArcUnderhand+(Aim_ArcUnderhandScale*Sin(-pitch)))
"####" E 0 A_TakeInventory("MiniMolly",1)
XH0D C 2 OffSet(1,31)
XH0D C 2 OffSet(-70,74)
XH0D C 2 OffSet(-121,114)
XH0D D 2 OffSet(1,31)
XH0D D 2 OffSet(-21,56)
XH0D D 2 OffSet(-33,84)
TNT1 A 16 OffSet(2,32)
"####" D 0 A_JumpIfInventory("MiniMolly",1,"Fire.Readyup")
"####" B 0 A_Jump(256,"End")
goto End

Fire.Readyup:
XH0D A 2 Offset(-4,69)
XH0D A 2 Offset(-2,48)
goto Ready1

End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

Actor MiniMollyFirehitDelay : powerprotection
{
Powerup.duration 8
Damagefactor "MiniMollyFire", 0
}


actor MiniMollythrow : BasicThrowable
{
Obituary "$OB_MINIMOLLY"
States
{
Spawn:
TNT1 A 0
NAPA A 0 A_GiveInventory("ProjectileCode",1)
NAPA A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
NAPA ABCD 1 A_SpawnItemEX("BasicThrowableTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
NAPA A 0 A_CountDown
NAPA A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn.Bounced:
TNT1 A 1 A_SpawnItemEX("MiniMollythrow2",0,0,0,momx*0.1,momy*0.1,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
NAPA ABCD 1 A_Jump(256,"Spawn.Bounced")
Goto Spawn.Bounced

Death:
MINB A 0 A_SpawnItemEx("MiniMollyExplosion",0,0,0,0,0,0,0,1)
stop
}
}

actor MiniMollythrow2 : MiniMollythrow
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
NAPA A  0 A_GiveInventory("ProjectileCode",1)
NAPA A  1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
NAPA A 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
NAPA A 1 A_CountDown
NAPA A 0 A_CheckFloor("SlowDown")
loop
SlowDown:
NAPA A 0 A_ScaleVelocity(0.3)
Goto Spawn2
	
Death:
MINB A 0 A_SpawnItemEx("MiniMollyExplosion",0,0,0,0,0,0,0,1)
stop
}
}

actor MiniMollyUnderthrow : MiniMollythrow
{Speed 25}

actor MiniMollyExplosion : BasicExplosion
{
Obituary "$OB_MINIMOLLY"
Scale 5.5
States
{
Spawn:
NAPC A 0 
MBAL B  0 A_GiveInventory("ProjectileCode",1)
SNAK A 0 A_SpawnItemEx("MiniMollyFireSpawner", 0,0,0, 1, 0, 0, Random(-15, 15), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SNAK A 0 A_SpawnItemEx("MiniMollyFireSpawner", 0,0,0, 1, 0, 0, Random(75, 105), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SNAK A 0 A_SpawnItemEx("MiniMollyFireSpawner", 0,0,0, 1, 0, 0, Random(165, 195), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SNAK A 0 A_SpawnItemEx("MiniMollyFireSpawner", 0,0,0, 1, 0, 0, Random(-75, -105), SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
TNT1 A 35 A_SpawnItemEx("ExplosionEffect2")
stop
}
}


actor MiniMollyFireSpawner
{
PROJECTILE
+RIPPER
+DONTREFLECT
+THRUGHOST
+ACTIVATEMCROSS
//+FLOORHUGGER
+NOEXPLODEFLOOR
+SLIDESONWALLS
Radius 5
Height 10
scale 2.5
speed 8
damage 0
damagetype "MiniMollyFire"
Obituary "$OB_MINIMOLLY"
maxstepheight 33
-NOGRAVITY
+DONTBLAST
+NOBLOOD
painchance 0
reactiontime 2
renderstyle none
States
{
Spawn:
SWHE A 0
SWHE A 0 A_SpawnItemEx("MiniMollyFireSpawner2", 0,0,0, 1, 0, 0, 90, SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SWHE A 0 A_SpawnItemEx("MiniMollyFireSpawner2", 0,0,0, 1, 0, 0, 270, SXF_TRANSFERTRANSLATION|SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
SWHE A 10
SWHE F 0 A_CountDown
Goto Spawn
Death:
TNT1 A 0 
stop
}
}

actor MiniMollyFireSpawner2 : MiniMollyFireSpawner
{
ReactionTime 2
States
{
Spawn:
SWHE A 0
SWHE ABCDEF 2 A_SpawnItemEx("MiniMollyFire", 0, 0, 0, Random(-5, 5),Random(-5, 5),0,0,SXF_TRANSFERTRANSLATION)
SWHE F 0 A_CountDown
Goto Spawn
Death:
TNT1 A 0 
stop
}
}

actor MiniMollyFire : MiniMollyFireSpawner
{
-FLOORHUGGER
//-RIPPER
+BRIGHT
Damage(5)
seesound "weapon/firestorm"
scale 2.5
renderstyle normal
reactiontime 20
States
{
Spawn:
BRNF A 0
BRNF A 0 A_PlaySoundEx("weapon/WaveBurner","Weapon")
Spawn2:
BRNF AB 2 
BRNF A 0 A_Stop
BRNF A 0 A_CountDown
Goto Spawn2
Death:
FLMB A 0 A_SpawnItemEx("MiniMollyFireFading",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_CLIENTSIDE)
stop
}
}

actor MiniMollyFireFading : MiniMollyFire
{
+NOINTERACTION
+CLIENTSIDEONLY
States
{
Spawn:
BRNF AB 2 A_FadeOut(0.05)
Loop
}
}